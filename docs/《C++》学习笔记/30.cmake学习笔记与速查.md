# CMake学习

> 主要参考[CMake菜谱](https://www.bookstack.cn/read/CMake-Cookbook/README.md)教程总结的学习笔记，仅供笔者参考，完整教程请移步[CMake菜谱](https://www.bookstack.cn/read/CMake-Cookbook/README.md) :)

## 从可执行文件到库

### 编译单个文件

```cmake
# 设置CMake所需的最低版本
cmake_minimum_required (VERSION 3.8)

# 声明项目的名称和支持的编程语言(CXX代表C++)
project(hello_cmake_world LANGUAGES CXX)

# 将源代码添加到此项目的可执行文件。
add_executable (hello-cmake "hello_cmake_world.cpp")
```

该文件与源文件`hello_cmake_world.cpp`放在一起，执行

```bash
mkdir -p build
cd build
cmake ..
cmake --build .
```

> **注意：**
>
> 1. CMake语言不区分大小写，但是参数是区分大小写的
>
> 2. CMake中，C++是默认的编程语言。不过，建议使用`LANGUAGES`选项在`project`命令中显式地声明项目的语言
>
> 3. 使用`cmake -help`查看完整的帮助信息
>
> 4. ```cmake
>    # mkdir -p sirius_build
>    # cd sirius_build
>    # cmake ..
>    # 以上构建步骤可用跨平台命令代替
>    # -H表示当前目录中搜索根CMakeLists.txt文件
>    # -Bsirius_build告诉CMake在一个名为sirius_build的目录中生成所有的文件
>    cmake -H. -Bsirius_build
>    ```

### 构建和链接静态库

```cmake
cmake_minimum_required (VERSION 3.8)
project(hello_cmake_world LANGUAGES CXX)

# 创建目标——静态库。库的名称和源码文件名相同
add_library(message
  STATIC
    "Message.hpp"
    "Message.cpp"
  )

add_executable (hello-cmake "hello_cmake_world.cpp")
# 最后需要将目标库链接到可执行目标
target_link_libraries(hello-cmake message)
```

### 构建和链接动态库

```cmake
# 与构建静态库一样，只需将STATIC改为SHARED
add_library(message
  SHARED
    "Message.hpp"
    "Message.cpp"
  )
```

如果考虑跨平台，可为动态库的头文件添加：

```c++
#pragma once

/* 添加一下内容以实现跨平台 */
#ifdef _WIN32
    #ifdef MessageDLL
        #define MessageDLL __declspec(dllexport)
    #else
        #define MessageDLL __declspec(dllimport)
    #endif
#else
    #define MessageDLL
#endif
//////////END////////////

class MessageDLL Message {
public:
    // ... DO SOMETING ...
};
```

### 条件句控制编译

先看一个例子：

```cmake
cmake_minimum_required(VERSION 3.8)
project(hello_cmake_world LANGUAGES CXX)

# 引入变量USE_LIBRARY，message打印相关信息
set(USE_LIBRARY OFF)
message(STATUS "Compile sources into a library? ${USE_LIBRARY}")
# BUILD_SHARED_LIBS在下面第二点有具体解释
set(BUILD_SHARED_LIBS OFF)

# 引入变量包括*.hpp\*.cpp文件
list(APPEND _sources "Message.hpp" "Message.cpp")

# 逻辑控制编译
if (USE_LIBRARY)
	add_library(message ${_sources})
	add_executable(hello-cmake "hello_cmake_world.cpp")
	target_link_libraries(hello-cmake message)
else()
	add_executable(hello-cmake "hello_cmake_world.cpp" ${_sources})
endif()
```

1. 使用`set`为变量设置值，其中逻辑变量可以是以下几种之一：
   + 逻辑`true`：`1`、`ON`、`YES`、`true`、`Y`或`非零数`；
   + 逻辑`false`：`0`、`OFF`、`NO`、`false`、`N`、`IGNORE`、`NOTFOUND`、`空字符串`或以`-NOTFOUND`为后缀。
2. `BUILD_SHARED_LIBS`是CMake的一个全局标志，可以决定为生成动态库还是静态库。如果为`false`或未定义，将生成一个静态库；
3. `_sources`变量是一个局部变量，不应该在当前范围之外使用，可以在名称前加下划线。

### 显示选项

> 上一节中（条件句控制编译）用硬编码控制逻辑，只能从内部修改。
>
> 本节使用`option()`命令，以选项的形式在外部设置编译逻辑

**语法：**

`option`可接受三个参数：

```cmake
option(<option_variable> "help string" [initial value])
```

- `<option_variable>`变量名
- `"help string"`在终端可见的帮助性文字
- `[initial value]`选项的默认值，可以是`ON`或`OFF`。

因而对于上一节中的`CMakeLists.txt`，只需将`set(USE_LIBRARY OFF)`使用`option()`代替，并设置默认值即可：

```cmake
option(USE_LIBRARY "Compile sources into a library" OFF)
```

---

如果有依赖关系，如：用户设置不构建库，但设置了构建动态库，此时应该不构建库。CMake提供`cmake_dependent_option()`命令用来定义依赖于其他选项的选项：

> 需要引入`CMakeDependentOption`模块

**语法：**

`cmake_dependent_option`可接受五个参数：

```cmake
cmake_dependent_option(<OPTION_NAME> "help string" <default_value> <depends> <force_value>)
```

- `<OPTION_NAME>`变量名

- `"help string"`帮助性文字

- `<default_value>`默认值

- `<depends>`该值为`true`时 **开启此选项**并设置为默认值，否则强制该选项默认值为 **<force_value>**

  `<depends>`可以用分号代表与的关系，例如：

  `"ENABLE_BUILD;NOT DISABLE_TESTING"`表示（`ENABLE_BUILD` && (！`DISABLE_TESTING`) )

因而添加`是否编译为库`和`是否编译为动态库`两个选项时，可修改为：

```cmake
option(USE_LIBRARY "Compile sources into a library" OFF)

include(CMakeDependentOption)
# 当USE_LIBRARY为ON时，MAKE_STATIC_LIBRARY是；MAKE_STATIC_LIBRARY是
# 当USE_LIBRARY为ON时，MAKE_STATIC_LIBRARY是；MAKE_STATIC_LIBRARY是
cmake_dependent_option(
	MAKE_STATIC_LIBRARY "Compile sources into a static library" OFF
	"USE_LIBRARY" ON
)

cmake_dependent_option(
	MAKE_SHARED_LIBRARY "Compile sources into a shared library" ON
	"USE_LIBRARY" ON
)
```

